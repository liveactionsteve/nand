// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/05/Memory.hdl

/**
 * The complete address space of the Hack computer's memory,
 * including RAM and memory-mapped I/O. 
 * The chip facilitates read and write operations, as follows:
 *     Read:  out(t) = Memory[address(t)](t)
 *     Write: if load(t-1) then Memory[address(t-1)](t) = in(t-1)
 * In words: the chip always outputs the value stored at the memory 
 * location specified by address. If load==1, the in value is loaded 
 * into the memory location specified by address. This value becomes 
 * available through the out output from the next time step onward.
 * Address space rules:
 * Only the upper 16K+8K+1 words of the Memory chip are used. 
 * Access to address>0x6000 is invalid. Access to any address in 
 * the range 0x4000-0x5FFF results in accessing the screen memory 
 * map. Access to address 0x6000 results in accessing the keyboard 
 * memory map. The behavior in these addresses is described in the 
 * Screen and Keyboard chip specifications given in the book.
 */

CHIP Memory {
    IN in[16], load, address[15];
    OUT out[16];

    PARTS:
    RAM16K(in=in, load=ramLoad, address=address[0..13], out=memout);
    Screen(in=in, load=scrLoad, address=address[0..12], out=scrout);
    Keyboard(out=kbdout);

    // The elegant way
    /* a[13..14] device    address
          00      RAM       a[0..13]
          01      RAM       a[0..13]
          10      Screen    a[0..12]
          11      Kbd         ---
    */
    DMux4Way(in=load, sel=address[13..14], a=ram00, b=ram01, c=scrLoad, d=unused);
    Or(a=ram00, b=ram01, out=ramLoad);
    Mux4Way16(a=memout, b=memout, c=scrout, d=kbdout, sel=address[13..14], out=out);

    // My way
    /*
    Not(in=address[14], out=lowmem);  // address is less than 0x4000
    And(a=load, b=lowmem, out=ramLoad);
    Not(in=address[13], out=notA13);
    And(a=address[14], b=notA13, out=screenmem);  // address is 10x xxxx xxxx xxxx, so in Screen range
    And(a=load, b=screenmem, out=scrLoad);
    // doesn't matter what we do when memory is greater than 0x6000, so just output Keyboard
    Mux4Way16(a=kbdout, b=memout, c=scrout, d=false, sel[0]=lowmem, sel[1]=screenmem, out=out);
    */
}